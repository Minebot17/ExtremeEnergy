buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

repositories {
    maven {
        name "HWYLA"
        url "http://tehnut.info/maven"
    }
    maven {
        name = "JEI"
        url "https://modmaven.k-4u.nl"
    }
    maven {
        name = "CoFH"
        url "https://maven.covers1624.net/"
    }
    maven {
        name = "player.to"
        url "http://maven.ic2.player.to"
    }
    maven {
        name = "Chicken Bones"
        url "http://chickenbones.net/maven/"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.2.1"
group = "ru.minebot.meem"
archivesBaseName = "ExtremeEnergy"

sourceCompatibility = targetCompatibility = '1.8'

compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2838"
    runDir = "run"
    mappings = "snapshot_20171003"

    // Just change project version
    replace "@VERSION@", version
    replaceIn "Reference.java"
}

dependencies {
    compile "mcp.mobius.waila:Hwyla:1.8.26-B41_1.12.2"
    compile "net.industrial-craft:industrialcraft-2:2.8.73-ex112:dev"
    compile "cofh:RedstoneFlux:1.12-2.1.0.6:deobf"
    compile "cofh:CoFHCore:1.12.2-4.6.1.22:deobf"
    compile "cofh:CoFHWorld:1.12.2-1.3.0.6:deobf"
    compile "cofh:ThermalExpansion:1.12.2-5.5.2.39:deobf"
    compile "cofh:ThermalFoundation:1.12.2-2.6.1.22:deobf"
    compile "codechicken:CodeChickenLib:1.12.2-3.2.2.353:deobf"
    compile("mezz.jei:jei_1.12.2:4.14.4.264") {
        // Dependencies contain multiple versions of JEI
        force = true
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        // Just change project version
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes 'FMLCorePlugin': 'ru.minebot.extreme_energy.hook.HookRegister'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
        attributes 'FMLAT': 'meem_at.cfg'
    }
}

apply plugin: 'idea'

idea {
    module {
        inheritOutputDirs=true
    }
}
